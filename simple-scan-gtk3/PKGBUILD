pkgname=simple-scan-gtk3
_pkgname=simple-scan
pkgver=44.0
pkgrel=2
pkgdesc="Simple scanning utility"
url="https://gitlab.gnome.org/GNOME/simple-scan"
arch=(x86_64)
license=(GPL3)
depends=(
  colord-sane
  dconf
  gtk3
  libgusb
  libhandy
  sane
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
conflicts=(
  simple-scan
)
provides=(
  simple-scan
)
_commit=85794e3a443f3817c60f4466ebcbc107d8a63a77  # tags/44.0^0
source=("git+https://gitlab.gnome.org/GNOME/simple-scan.git#commit=$_commit")
b2sums=('SKIP')

prepare() {
  cd $_pkgname
}

build() {
  arch-meson $_pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:


# Maintainer: kj_sh604 <406hs_jk at proton dot me>

pkgname=coreutils-uutils
pkgver=0.1.0
pkgrel=5
pkgdesc='(Dangerous) Use uutils as system coreutils'
arch=('x86_64')
license=('MIT')
url=https://github.com/uutils/coreutils
conflicts=(coreutils b3sum sha3sum)
provides=(coreutils b3sum)
depends=(uutils-coreutils=${pkgver}) # protect user from change of uutils pkg
makedepends=(rust-musl make)
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=('55c528f2b53c1b30cb704550131a806e84721c87b3707b588a961a6c97f110d8')

build() {
  cd coreutils-${pkgver}
  cargo build -p uu_stty --release --target=x86_64-unknown-linux-musl # missing docs. But multicall build failds.
  #cd src/uu/stdbuf/src/libstdbuf
  #cargo build --release
}

package() {
  cd coreutils-$pkgver
  install -Dm755 target/x86_64-unknown-linux-musl/release/stty "$pkgdir"/usr/bin/stty
  #install -Dm644 target/release/liblibstdbuf.so "$pkgdir"/usr/lib/coreutils/libstdbuf.so # next release has this
  # fail if uu-coreutils was renamed at uutils-coreutils
  /usr/bin/uu-coreutils install -d "$pkgdir"/usr/{bin,share/{man/man1,zsh/site-functions,fish/vendor_completions.d}}
  cd "$pkgdir"/usr
  cp -sf /usr/bin/uu-coreutils bin/\[ # avoid completion err
  # Don't symlink /usr/bin/coreutils. Supoort every uutils packages.
  for f in $(uu-coreutils --list|grep -v -E '^(kill|more|uptime|hostname|\[)$')  chcon runcon ; do
    ln -sf /usr/bin/uu-coreutils bin/"$f"
    ln -sf /usr/share/man/man1/uu-"$f".1.gz share/man/man1/"$f".1.gz
    echo -e "#compdef ${f}=uu-${f}\n_uu-${f}" > share/zsh/site-functions/_$f
    echo "complete -c ${f} -w uu-${f}" > share/fish/vendor_completions.d/${f}.fish
  done
}

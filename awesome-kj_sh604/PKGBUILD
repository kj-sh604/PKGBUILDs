# Maintainer: kj_sh604 <406hs_jk@proton.me>

pkgname=awesome-kj_sh604
_pkgname=awesome
pkgver=24.10.27
pkgrel=1
pkgdesc='this is just a renamed awesomeWM build, customized to suit
         my personal preferences on my system. (tagged release)'
_commit='fcd23a7478b38788e13849f76190e60338ba9fdc'
arch=('i686' 'x86_64')
url='https://awesomewm.org'
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'libxdg-basedir' 'libxkbcommon-x11'
         'lua' 'lua-lgi' 'pango' 'startup-notification' 'xcb-util-cursor'
         'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-xrm' 'libxfixes')
makedepends=('asciidoctor' 'cmake' 'docbook-xsl' 'git' 'imagemagick' 'ldoc'
             'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'xcb-util-errors: for pretty-printing of X11 errors'
            'librsvg: for displaying SVG files without scaling artifacts'
)
provides=('notification-daemon' 'awesome')
conflicts=('awesome')
backup=('etc/xdg/awesome/rc.lua')
source=("$_pkgname::git+https://github.com/kj-sh604/awesome.git#commit=$_commit")
md5sums=('SKIP')
_LUA_VER=5.4

build() {
  mkdir -p build
  cd build
  cmake ../$_pkgname \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_INCLUDE_DIR=/usr/include/lua${_LUA_VER} \
    -DLUA_LIBRARY=/usr/lib/liblua.so.${_LUA_VER} \
    -DLUA_EXECUTABLE=/usr/bin/lua${_LUA_VER}
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$_pkgname/awesome.desktop \
    "$pkgdir/usr/share/xsessions/awesome.desktop"
}

# Maintainer: kj_sh604 <406hs_jk@proton.me>
# Contributor: Bernhard Landauer <oberon@manjaro.org>

pkgname=xfce4-volumed-pulse
pkgver=0.2.4
pkgrel=4
pkgdesc="A volume keys control daemon for Xfce using pulseaudio"
arch=('x86_64')
url="https://gitlab.xfce.org/apps/xfce4-volumed-pulse"
license=('GPL-3.0-only')
depends=('libnotify' 'pulse-native-provider' 'libkeybinder3' 'xfconf')
makedepends=('git' 'xfce4-dev-tools')
optdepends=('xfce4-notifyd: for OSD notifications')
source=("git+https://gitlab.xfce.org/apps/xfce4-volumed-pulse.git#tag=$pkgname-$pkgver")
sha256sums=('73d38fcf931c850101f0a772889c196c142a77b10e9647b46ebded70d89fac7e')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:

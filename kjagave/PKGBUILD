pkgname=kjagave-git
pkgver=r15.55b9987
pkgrel=1
pkgdesc="a color picker inspired by agave, but only with the features that kj_sh604 used"
arch=('x86_64')
url="https://github.com/kj-sh604/kjagave"
license=('GPL2')
depends=('gtk3')
optdepends=('xcolor: for screen color picking')
makedepends=('go' 'git')
provides=('kjagave')
conflicts=('kjagave')
source=("git+https://github.com/kj-sh604/kjagave.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/kjagave"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/kjagave/src"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"
  go mod download
  go mod download github.com/gotk3/gotk3
  go build -x -o kjagave main.go
}

package() {
  cd "${srcdir}/kjagave"
  install -Dm755 "src/kjagave" "${pkgdir}/usr/bin/kjagave"
  install -Dm644 "archlinux/kjagave.desktop" "${pkgdir}/usr/share/applications/kjagave.desktop"
  install -Dm644 "archlinux/kjagave.png" "${pkgdir}/usr/share/pixmaps/kjagave.png"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/kjagave/LICENSE"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/kjagave/README.md"
}

# vim:set ts=2 sw=2 et:

pkgname=acpilight-kj_sh604
pkgver=1.2
pkgrel=3
_commit='1d892d600a00aece87c6c5588bce93721c898acb'
pkgdesc="A backward-compatible xbacklight replacement based on ACPI"
arch=('any')
url="https://github.com/kj-sh604/${pkgname%-kj_sh604}"
license=('GPL-3.0-only')
depends=('python')
makedepends=('git')
provides=('xorg-xbacklight')
conflicts=('xorg-xbacklight' 'acpilight')
source=("${pkgname%-kj_sh604}::git+${url}.git#commit=${_commit}")
install="${pkgname%-kj_sh604}.install"
sha256sums=('b5db8ddffd7df5b314618c77edf17f2cc611636efee62fd31afb0e3d9a546c06')

package() {
  cd "${pkgname%-kj_sh604}"
  install -vDm0755 xbacklight "${pkgdir}/usr/bin/xbacklight"
  install -vDm0644 xbacklight.1 "${pkgdir}/usr/share/man/man1/xbacklight.1"
  install -vDm0644 90-backlight.rules "${pkgdir}/usr/lib/udev/rules.d/90-backlight.rules"
  install -vDm0644 README.rst NEWS.rst -t "${pkgdir}/usr/share/doc/${pkgname%-kj_sh604}"
}

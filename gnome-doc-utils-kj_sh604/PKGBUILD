# Maintainer: Patrick Northon <northon_patrick3@yahoo.ca>
# Contributor: Brian Bidulock <bidulock@openss7.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-doc-utils-kj_sh604
_pkgname=gnome-doc-utils
pkgver=0.20.10+16+gc03cc09
pkgrel=4
pkgdesc="Documentation utilities for Gnome"
arch=('any')
license=('GPL' 'LGPL')
depends=(docbook-xml rarian python2-libxml2)
makedepends=(intltool gnome-common git)
provides=(gnome-doc-utils)
conflicts=(gnome-doc-utils)
url="https://www.gnome.org"
_commit=c03cc0963996934e03587af3e351c2bb9dab6b2a  # master
source=("git+https://git.gnome.org/browse/gnome-doc-utils#commit=$_commit")
source=("git+https://gitlab.gnome.org/Archive/gnome-doc-utils.git#commit=$_commit")
sha256sums=('91886a569a503d0b827f278656872036f7c6e551087603003fdab14615a1dbbd')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname
  rm -f m4/glib-gettext.m4
  sed -i 's/SUBDIRS = .*/SUBDIRS = /' 'doc/Makefile.am'
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$_pkgname"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sysconfdir=/etc --mandir=/usr/share/man \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "$_pkgname"
  make DESTDIR="$pkgdir" install
}

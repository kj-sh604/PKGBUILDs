# Maintainer: kj_sh604 <406hs_jk@proton.me>

pkgname=coreutils-uutils-selinux
pkgver=0.1.0
pkgrel=3
pkgdesc='Cross-platform Rust rewrite of the GNU coreutils installed as system
core utilities with SELinux support (WARNING: use at own risk).'
arch=('x86_64')
license=('MIT')
url='https://github.com/uutils/coreutils'
conflicts=(
  coreutils
  b3sum
  sha3sum
)
provides=(
  coreutils
  b3sum
  sha3sum
)
depends=(
  gcc-libs
  glibc
  libselinux
  oniguruma
)
makedepends=(
  cargo
  clang
  python-sphinx
  rust
)
source=("uutils-coreutils-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
         update-progs.patch)
sha256sums=('55c528f2b53c1b30cb704550131a806e84721c87b3707b588a961a6c97f110d8'
            '0bd635cc349cfe133ddad0b1d05256c023b46f4a30f81aee123100119762df8e')

 prepare() {
  cd ${pkgname%-uutils-selinux}-${pkgver}
  # patch PROGS to include stty
  patch -Np1 -i $srcdir/update-progs.patch
}

package() {
  cd ${pkgname%-uutils-selinux}-${pkgver}
  export RUSTONIG_DYNAMIC_LIBONIG=1
  make install \
      USE=selinux \
      DESTDIR="$pkgdir" \
      PREFIX=/usr \
      MANDIR=/share/man/man1 \
      PROG_PREFIX= \
      PROFILE=release \
      MULTICALL=y \
      SKIP_UTILS="hostname kill more uptime"
  # add libstdbuf.so
  mkdir -p $pkgdir/usr/lib/coreutils
  cd $srcdir && cd ${pkgname%-uutils-selinux}-${pkgver}/target/release/deps
  mv liblibstdbuf.so $pkgdir/usr/lib/coreutils/libstdbuf.so
  # clean conflicts (arch ships these in the bash-completion package)
  rm $pkgdir/usr/share/bash-completion/completions/*
  # symlink missing binaries
  cd $pkgdir/usr/bin
  if [ -f "coreutils" ]; then
    local binaries=(
      "b2sum" "b3sum" "md5sum" "sha1sum" "sha224sum" "sha256sum" "sha3-224sum"
      "sha3-256sum" "sha3-384sum" "sha3-512sum" "sha384sum" "sha3sum"
      "sha512sum" "shake128sum" "shake256sum"
    )
    for bin in "${binaries[@]}"; do
      ln -s coreutils "$bin" || echo "warning: failed to create symlink for $bin"
    done
  else
    echo "coreutils binary not found, skipping symlink creation."
  fi
}

# vim: ts=2 sw=2 et:

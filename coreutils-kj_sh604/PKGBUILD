pkgname=coreutils-kj_sh604
pkgver=9.9
pkgrel=1
pkgdesc='The basic file, shell and text manipulation utilities of the GNU
operating system (with the `arch` binary, managed by kj_sh604)'
arch=('x86_64')
license=(
  GPL-3.0-or-later
  GFDL-1.3-or-later
)
url='https://www.gnu.org/software/coreutils/'
depends=( 
  acl  
  attr
  glibc
  gmp
  libcap
  openssl
)
makedepends=(
  git
  gperf
  python
  wget
)
conflicts=(
    coreutils
    coreutils-arch
)
provides=(
    coreutils
    coreutils-arch
)
source=(
  https://ftp.gnu.org/gnu/coreutils/coreutils-${pkgver}.tar.xz
)
options=(!lto)
b2sums=('ddae0c1ff882739712e901d6eb1373e4172de9335b95b4c1004773aadc6b7e3cb3bf5aa583f13034062a75dc9194bf8534b0f7f1258c8ff64595b1301964e124')

prepare() {
  cd "${pkgname%-kj_sh604}-${pkgver}"
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd "${pkgname%-kj_sh604}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --with-openssl \
    --enable-install-program=arch \
    --enable-no-install-program=hostname,kill,uptime
  make
}

package() {
  cd "${pkgname%-kj_sh604}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
